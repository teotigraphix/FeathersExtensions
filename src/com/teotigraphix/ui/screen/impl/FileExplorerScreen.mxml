<?xml version="1.0"?>
<!--
////////////////////////////////////////////////////////////////////////////////
// Copyright 2015 Michael Schmalle - Teoti Graphix, LLC
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License
//
// Author: Michael Schmalle, Principal Architect
// mschmalle at teotigraphix dot com
////////////////////////////////////////////////////////////////////////////////
-->
<f:Screen xmlns:f="library://ns.feathersui.com/mxml"
          xmlns:fx="http://ns.adobe.com/mxml/2009"
          xmlns:component="com.teotigraphix.ui.component.*"
          xmlns:file="com.teotigraphix.ui.component.file.*">

    <f:layout>
        <f:VerticalLayout/>
    </f:layout>

    <component:HGroup percentWidth="100"
                      styleName="{STYLE_HEADER}">

        <f:Label id="titleLabel"
                 styleName="{FrameworkStyleNames.THEME_LABEL_ACTION_BAR}"/>

    </component:HGroup>

    <file:FileList id="fileList"
                   change="fileList_changeHandler()">
        <file:layoutData>
            <f:VerticalLayoutData percentWidth="100" percentHeight="100"/>
        </file:layoutData>
    </file:FileList>

    <component:HDivider/>

    <!-- Footer -->
    <component:HGroup id="footer"
                      styleName="{FrameworkStyleNames.DIALOG_BUTTON_FOOTER}">

        <f:Button id="okButton"
                  isEnabled="false"
                  styleName="{FrameworkStyleNames.FORM_OK_BUTTON}"
                  triggered="okButton_triggeredHandler()">
        </f:Button>
        <f:Button id="cancelButton"
                  styleName="{FrameworkStyleNames.FORM_CANCEL_BUTTON}"
                  triggered="cancelButton_triggeredHandler()">
        </f:Button>
        
    </component:HGroup>

    <fx:Script><![CDATA[
        import com.teotigraphix.ui.component.file.FileListData;
        import com.teotigraphix.ui.theme.framework.FrameworkStyleNames;

        import feathers.skins.IStyleProvider;

        public static const EVENT_OK:String = "okTap";
        public static const EVENT_CANCEL:String = "cancelTap";

        /**
         * Dispatches when a file or directory has changed.
         */
        public static const EVENT_FILE_OR_DIRECTORY_CHANGE:String = "fileOrDirectoryChange";
        public static const STYLE_HEADER:String = "file-screen-header";

        public static var globalStyleProvider:IStyleProvider;

        private var _title:String;
        private var _data:FileListData;

        override protected function get defaultStyleProvider():IStyleProvider
        {
            return FileExplorerScreen.globalStyleProvider;
        }

        //--------------------------------------------------------------------------
        // Public :: Properties
        //--------------------------------------------------------------------------

        //----------------------------------
        // title
        //----------------------------------

        public function get title():String
        {
            return _title;
        }

        public function set title(value:String):void
        {
            _title = value;
            titleLabel.text = _title;
            titleLabel.width = NaN;
        }

        //----------------------------------
        // data
        //----------------------------------

        public function get data():FileListData
        {
            return _data;
        }

        public function set data(value:FileListData):void
        {
            _data = value;
            invalidate(INVALIDATION_FLAG_DATA);
        }

        //----------------------------------
        // selectedFile
        //----------------------------------

        public function get selectedFile():File
        {
            return fileList.rootDirectory;
        }

        //--------------------------------------------------------------------------
        // Methods
        //--------------------------------------------------------------------------

        override protected function draw():void
        {
            super.draw();

            if (isInvalid(INVALIDATION_FLAG_DATA))
            {
                if (_data != null)
                {
                    title = _data.title;
                    _data.copyTo(fileList);
                }
            }
        }

        //--------------------------------------------------------------------------
        // Handlers
        //--------------------------------------------------------------------------

        private function okButton_triggeredHandler():void
        {
            dispatchEventWith(EVENT_OK, false, selectedFile);
        }

        private function cancelButton_triggeredHandler():void
        {
            dispatchEventWith(EVENT_CANCEL, false, selectedFile);
        }

        private function fileList_changeHandler():void
        {
            dispatchEventWith(EVENT_FILE_OR_DIRECTORY_CHANGE, false, selectedFile);
        }
        ]]></fx:Script>
</f:Screen>